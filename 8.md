% 関数型勉強会 第8回
% Kazuyoshi Osanai
% Jul 23, 2013

## 今回の範囲

- 前回の練習問題の解答
- 7章後半のスライド
- 7章後半に関する練習問題

# 練習問題の解答

## 前回のテーマ

- 基本的な型定義ができるようになる
    + 型引数を含まない型
    + レコード構文の使い時

- 型引数を含む型定義を利用できるようになる
    + 型引数の使い時

## 1-1. じゃんけんの手を表す型`Hand`と勝敗を表す型`VS`を作り，2つの`Hand`の勝敗を返す関数を作りなさい．`VS`は勝ち，負け，引き分けのいずれかの状態を表すとする．

~~~ {.haskell}
data Hand = Goo | Choki | Par
          deriving (Eq,Ord,Enum,Show)
data VS = Win | Lose | Draw
        deriving (Eq,Ord,Enum,Show)

judge :: Hand -> Hand -> VS
judge Goo Choki = Win
judge Goo Par = Lose
judge Choki Goo = Lose
judge Choki Par = Win
judge Par Goo = Win
judge Par Choki = Lose
judge x y | x == y = Draw
          | otherwise = error "can't judge"
~~~

- パターンマッチは上からチェックしていくことに注意

## (続)1-1. じゃんけんの手を表す型`Hand`と勝敗を表す型`VS`を作り，2つの`Hand`の勝敗を返す関数を作りなさい．`VS`は勝ち，負け，引き分けのいずれかの状態を表すとする．

~~~ {.haskell}
ghci> let combinations = [(x,y) | x<-[Goo .. Par], y<-[Goo .. Par]]
ghci> combinations
[(Goo,Goo),(Goo,Choki),(Goo,Par),(Choki,Goo),(Choki,Choki),(Choki,Par),(Par,Goo),(Par,Choki),(Par,Par)]
ghci> map (uncurry judge) combinations
[Draw,Win,Lose,Lose,Draw,Win,Win,Lose,Draw]
~~~

- `Enum`のインスタンスを自動導出したことで，\\  
`Hand`は`".."`によるリスト生成ができる
- `uncurry :: (a -> b -> c) -> (a, b) -> c`は，
    + 「1引数->1引数->戻り値」の関数を
    + 「タプル引数->戻り値」に変える

## 1-2. RGB値(各256階調)を格納する色を表す型`Color`を作り，1つの`Color`の補色を計算する関数を作りなさい．補色の計算は，各RGB値から255を引いて絶対値を取ったものとする．

~~~ {.haskell}
data Color = RGB Double Double Double |
             CMYK Double Double Double Double
           deriving (Eq,Show)

complementary :: Color -> Color
complementary (RGB r g b) = RGB (comp r) (comp g) (comp b)
  where
    comp = abs . (flip (-) 255)
~~~

- 型名と値コンストラクタ名は同じでなくてもよい
- `CMYK`は参考用

## 2-1. 2x2行列を表す型`TwoSquareMat a`を作り，`TwoSquareMat a`の転置行列を返す関数を作りなさい．

~~~ {.haskell}
data TwoSquareMat a = TSMat a a a a
                  deriving (Eq)

instance (Show a) => Show (TwoSquareMat a) where
  show (TSMat x1 y1 x2 y2) 
    =  "Mat { " ++ show x1 ++ " " ++ show y1 ++ "\n" 
    ++ "      " ++ show x2 ++ " " ++ show y2 ++ " }"

transpose' :: TwoSquareMat a -> TwoSquareMat a
transpose' (TSMat x1 y1 x2 y2) = TSMat x1 x2 y1 y2

ghci> transpose' $ TSMat 1 2 3 4
Mat { 1 3
      2 4 }
~~~

<!-- ' -->

- 自動導出させずに自分で実装してもよい
    + 具体的なインスタンスの実装方法は今回の範囲

# 型や型クラス(後半)

## 今回のテーマ

- 基本的な型定義ができるようになる
    + 型引数を含まない型
    + レコード構文の使い時

- 型引数を含む型定義を利用できるようになる
    + 型引数の使い時

## 型シノニム

- `type`で型に別名をつけることができる
- 型シノニムは型が使えるところで同じように使える
- 使いどころ:
    + 何度も複雑な同じ型名が現われるとき
        + `type StudentMap = Map Int String`など
    + ソースコードのドキュメント性を高めたいとき

~~~ {.haskell}
type Height = Double
readHeight :: String -> Height
~~~

## 再帰的なデータ構造

- `data`キーワードで再帰的なデータ構造を定義できる
- 値コンストラクタを中置関数にする場合，コロンで始まる必要がある

~~~ {.haskell}
data List a = EmptyList | a :-: List a
headList :: List a -> a
headList (x :-: _) = x

data BinTree a = EmptyTree | BinTree a (Tree a) (Tree a)
~~~

## 型クラス

- JavaとかRubyのクラスとは異なる 
- ある型についての特定の振る舞いを定義する
    + 等値性判断，順序比較など

~~~ {.haskell}
class  Eq a  where
    (==), (/=)           :: a -> a -> Bool

    x /= y               = not (x == y)
    x == y               = not (x /= y)
~~~

- `Eq`クラスの関数は片方を定義すればもう片方も求まる
    + → どちらかを定義すれば`完全最小定義`になる

## 続・型クラス

- `instance`キーワードでインスタンス化する

~~~ {.haskell}
data TrafficLight = Red | Yellow | Green

instance Eq TrafficLight where
    Red == Red   = True
    Red == Green = False
    ...
    ...
~~~

<!-- ## `YesNo`型クラス -->

## `Functor`

- 写す(map over)ことに関する型クラス
- `Functor`の理解は`Monad`や`Applicative`の理解への足掛かりになる
- リストについては`map`と同様の動作
    + `Maybe`については？ `Either`については？
    + `:i Functor`でどんなインスタンスがあるか見てみよう

~~~ {.haskell}
class Functor f where
  fmap :: (a -> b) -> f a -> f b
~~~

## カインド

- 種類．型の型

~~~ {.haskell}
ghci> :kind Int
Int :: *
ghci> :k Maybe
Maybe :: * -> *
ghci> :m +Data.Map
ghci> :k Data.Map.Map
Data.Map.Map :: * -> * -> *
~~~

# 練習問題

## 1-1. 以下の名前を型の世界で使えるものと値の世界で使えるものに分別せよ

- `Display, LiquidCrystal, Plasma, CRT, ToDisplay a, Vector2d a, V2, IntVector2d`

~~~ {.haskell}
data Display = LiquidCrystal | Plasma | CRT
type ToDisplay a = a -> Display
data Vector2d a = V2 a a
type IntVector2d = Vector2d Int
~~~

## 2-1. ノードが任意の個数の葉を持つような木構造の型`Tree`を実装せよ

~~~ {.haskell}
singleton :: a -> Tree a
treeElem :: (Eq a) => Tree a -> a -> Bool
~~~

## 3-1. `Ord`を最小完全定義するにはどれを上書きすればよいか(複数回答可)

~~~ {.haskell}
class  (Eq a) => Ord a  where
  compare              :: a -> a -> Ordering
  (<), (<=), (>=), (>) :: a -> a -> Bool
  max, min             :: a -> a -> a

  compare x y | x == y    = EQ
              | x <= y    = LT
              | otherwise = GT

  x <= y =  compare x y /= GT
  x <  y =  compare x y == LT
  x >= y =  compare x y /= LT
  x >  y =  compare x y == GT

  max x y | x >= y    =  x
          | otherwise =  y
  min x y | x <  y    =  x
          | otherwise =  y
~~~

<!-- ## 4-1. 2-1で定義した`Tree`を`YesNo`のインスタンスにせよ -->

## 4-1. `Tree`を`Functor`のインスタンスにせよ

