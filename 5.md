% 関数型勉強会 第5回
% Kazuyoshi Osanai
% Jun 11, 2013

## 今回の範囲

- 前回の練習問題の解答
- 4章のスライド
- 4章と5章に関する練習問題

# 練習問題の解答

## 練習問題1-1

- 1-1. 数値のリストの先頭3つの値を足した値を返す関数`addHeadThree`を実装せよ．エラーとかは考えなくてよい．(パターンマッチ)

リストに対するパターンマッチの問題

~~~ {.haskell}
addHeadThree :: (Num a) => [a] -> a
addHeadThree (x:y:z:_) = x + y + z
~~~

## 練習問題1-2

- 1-2. `Show`型クラスのインスタンスのリストを受け取り，すべての要素を文字列にして連結した1つの文字列を返す関数`listToString`を実装せよ．(パターンマッチ)

`Show`型クラスのインスタンスには`show`関数が使える

~~~ {.haskell}
listToString :: (Show a) => [a] -> String
listToString [] = []
listToString (x:xs) = (show x) ++ listToString xs
~~~

## 練習問題1-3

- 1-3. 比較値とリストを受け取り，リストから比較値と等しい要素がいくつあるかカウントする関数`countThat`を実装せよ．(パターンマッチ，ガード)

ガードを使って条件分岐を書く

~~~ {.haskell}
countThat :: (Eq a) => a -> [a] -> Int
countThat _ [] = 0
countThat x (y:ys) | x == y = 1 + countThat x ys
                   | otherwise = countThat x ys
~~~

## 練習問題2-1

- 2-1. 2つの2次元ベクトルを受け取り，それらの和を返す関数`addV2`を実装せよ．(パターンマッチ)

タプルに対するパターンマッチ

~~~ {.haskell}
addV2 :: (Double,Double) -> (Double,Double) -> (Double,Double)
addV2 (x1,x2) (y1,y2) = (x1+y1,x2+y2)
~~~

## 練習問題2-2

- 2-2. ラジアン角度と2次元ベクトルを受け取り，ベクトルをラジアン角度で回転させた値を返す関数`rotateV2`を実装せよ．(パターンマッチ，`where`)

`where`を使うと関数定義内で複数回使う式をまとめられる．
`sin, cos`は`Double`の親クラス`Floating`で定義されている．

~~~ {.haskell}
rotateV2 :: Double -> (Double,Double) -> (Double,Double)
rotateV2 theta (x,y) = (x*c - y*s, x*s + y*c)
    where
        c = cos theta
        s = sin theta
~~~

## 練習問題2-3

- 2-3. 2次元座標値を受け取り，それが第何象限にあるか返す関数`quadrant`を実装せよ．(パターンマッチ，ガード)

タプルをパターンマッチで分解してガードの条件分岐にかける

~~~ {.haskell}
quadrant :: (Double,Double) -> Int
quadrant (x,y) | (x>=0) && (y>=0) = 1
               | (x<=0) && (y>=0) = 2
               | (x<=0) && (y<=0) = 3
               | (x>=0) && (y<=0) = 4
~~~

## 練習問題3-1

- 3-1. 2つの値を受け取り，比較の結果によって次の値を返す関数`describeComparison`を実装せよ．(`where`, `case`)

    + 等しい: [Aの値] equal to [Bの値].
    + 1つ目の値が小さい: [Aの値] is smaller than [Bの値].
    + 1つ目の値が大きい: [Aの値] is larger than [Bの値].

比較関係の型クラスには`Eq, Ord`がある．
`Ord`の関数`compare`を使えば「大きい」「等しい」「小さい」の場合分けができる．
`<`演算子を使ってもよい．

~~~ {.haskell}
describeComparison :: (Ord a, Show a) => a -> a -> String
describeComparison x y = show x ++ " " ++ comp ++ " " ++ show y ++ "."
    where comp = case compare x y of
                  EQ -> "equal to"
                  LT -> "is smaller than"
                  GT -> "is larger than"
~~~

# 再帰の考え方

## 基底部

- 再帰を考えるときは，これ以上分解できない<span class="green">基底部</span>をまず考える
    + フィボナッチ数列の基底部: F(0)=0, F(1)=1

- 基底部ができたら，それ以外の部分
    + フィボナッチ数列: F(n)=F(n-1)+F(n-2)
    + 最終的に基底部だけで表せたらOK: F(3) = F(2)+F(1) = F(1)+F(0)+F(1)

- Haskellでは「どうやるか」ではなく「何であるか」を宣言する

## 基底部の練習: `maximum`

- リストから最大の要素をとる`maximum`関数を考える

- 基底部は?

## 基底部の練習: `maximum`

- 基底部は，
    + 要素が0のとき
    + 要素が1のとき

- `max`は2引数をとり大きい方を返す関数である

~~~ {.haskell}
maximum' :: (Ord a) => [a] -> a
maximum' [] = error "try to get maximum of empty list"
maximum' [x] = x
maximum' (x:xs) = max x (mamimum' xs)
~~~

<!-- ' -->

## クイックソートは何をやっているのか?

- 最初に見せたクイックソートのコード

~~~ {.haskell}
quicksort :: Ord a => [a] -> [a]
quicksort [] = []
quicksort (x:xs) = smaller ++ [x] ++ greater
  where smaller = quicksort [y | y<-xs, y<=x]
        greater = quicksort [y | y<-xs, y>x]
~~~

## クイックソートは何をやっているのか?

- 基底部: "空リスト`[]`はソートしても空リスト`[]`"
- 再帰部:
    + リストの先頭要素`x`ピボットに，
    + `x`の前に`x`より小さいもののソート列，
    + `x`の後に`x`より大きいもののソート列を連結する
    + `x`より小さい/大きいものはリスト内包表記で取得し，再帰にかけてソート

~~~ {.haskell}
quicksort :: Ord a => [a] -> [a]
quicksort [] = []
quicksort (x:xs) = smaller ++ [x] ++ greater
  where smaller = quicksort [y | y<-xs, y<=x]
        greater = quicksort [y | y<-xs, y>x]
~~~

## クイックソートの疑問

- 基底部これだけ?

- これも基底部なんじゃね?

~~~ {.haskell}
quicksort [] = []
quicksort [x] = [x]
quicksort (x:y:[]) | x > y = [y,x]
                   | otherwise = [x,y]
~~~

## クイックソートの疑問

- 再帰部を書くと，基底部は空リストだけで十分だとわかる

- 迷ったときはとりあえず基底部をできるだけ書いて，後からいらないものを消す方針でよい

## 再帰が分からない人は...

- 山本和彦さんの「再帰ドリル」がおすすめ
    + 再帰ドリル: https://github.com/kazu-yamamoto/recursion-drill
    + Emacsのメーラー`Mew`の作者
    + Haskell製Webサーバ`Mighttpd2`の作者

# 練習問題

## 今回の練習問題

- `foldl`や`foldr`を使って，できるだけ明示的な再帰を書かずにやってみよう

- リスト内包表記を使わずに，`map`や`filter`で書いてみよう

- `$`や`.`や`flip`やラムダ式を使って，できるだけポイントフリースタイルで書いてみよう
    + ポイントフリースタイル: できるだけ変数を使わずに関数の連結でなんとかするスタイル

## 練習問題1

以下の関数を実装しなさい

- 1-1. リストの最小の要素を返す`minimum`
- 1-2. リストを反転する`reverse`
- 1.3. リストからn個の要素を取る`take`

~~~ {.haskell}
minimum' :: (Ord a) => [a] -> a
reverse' :: [a] -> [a]
take' :: Int -> [a] -> [a]
~~~

<!-- ' -->

## 練習問題2

以下の関数を実装しなさい

- 2-1. `Int`のリストを受け取り，奇数かつ10以下の要素のみを抜き出して，すべての要素を2倍する関数
- 2-2. `Int`の値`n`を受け取り，2以上16以下の偶数からなるリストのそれぞれの要素で`n`を割った値のリストを返す関数(余り切り捨て)

~~~ {.haskell}
smallOddTwice :: [Int] -> [Int]
divByEvens :: Int -> [Int]
~~~

## 練習問題3

以下の関数を実装しなさい

- 3-1. リストの最初の要素を初期値とする`foldr`，`foldr1`
- 3-2. `foldl`を`foldr`で実装しなさい

~~~ {.haskell}
foldr1 :: (a -> b > b) -> [b] -> a
foldl' :: (a -> b -> a) -> a -> [b] -> a
~~~

